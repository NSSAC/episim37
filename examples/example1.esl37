# Example 1: In-school NPIs

config enable_hybrid_learning: bool = True
config enable_day_30_antigen_test: bool = True
config enable_day_30_pcr_test: bool = True

config num_ticks: int = 10
global cur_tick: int = 0

node
    pid: int node key static
    is_in_school: bool static
    home_isolation_start: int
    home_isolation_end: int
end

edge
    target_pid: int target node key static
    source_pid: int source node key static
    duration: int static
    is_school_edge: bool static
    is_non_home_edge: bool static
end

enum c1_state_t
    S, E, Ipresymp, Isymp, Iasymp, R
end

contagion c1
    state type c1_state_t

    transition
        E -> Ipresymp, p = 0.65, dwell = fixed3
        E -> Iasymp, p = 0.35, dwell = normal5
        Ipresymp -> Isymp, dwell = fixed2
        Isymp -> R, dwell = isymp_r_dwell
        Iasymp -> R, dwell = normal5
    end

    def susceptibility(v: node) -> float:
        if v.c1.state == S:
            return 1.0
        else:
            return 0.0
        end
    end

    def infectivity(v: node) -> float:
        if v.c1.state == Isymp or v.c1.state == Iasymp:
            return 1.0
        else:
            return 0.0
        end
    end

    def transmissibility(e: edge) -> float:
        return 0.03 * e.duration
    end

    transmission
        Ipresymp => S -> E
        Isymp => S -> E
        Iasymp => S -> E
    end

    def enabled(e: edge) -> bool:
        if e.is_non_home_edge and (is_isolating(e.source_node) or is_isolating(e.target_node)):
            return False
        elif e.is_school_edge and not is_school_day():
            return False
        else:
            return True
        end
    end
end

distribution
    constant fixed2
        v = 2.0
    end

    constant fixed3
        v = 3.0
    end

    discrete isymp_r_dwell
        p = 0.175, v = 1
        p = 0.175, v = 2
        p = 0.1, v = 3
        p = 0.1, v = 4
        p = 0.1, v = 5
        p = 0.1, v = 6
        p = 0.1, v = 7
        p = 0.05, v = 8
        p = 0.05, v = 9
        p = 0.05, v = 10
    end

    normal normal5
        mean = 5.0, std = 1.0, min = 0.0
    end
end


def is_isolating(n: node) -> bool:
    if cur_tick <= n.home_isolation_start and n.home_isolation_end < cur_tick:
        return True
    else:
        return False
    end
end

# Monday = 1
# Tuesday = 2
# Wednesday = 3
# Thursday = 4
# Friday = 5
def is_school_day() -> bool:
    # const day_of_week : int = cur_tick % 7
    var day_of_week : int = cur_tick % 7

    if enable_hybrid_learning and 1 <= day_of_week and day_of_week <= 3:
        return True
    elif 1 <= day_of_week and day_of_week <= 5:
        return True
    else:
        return False
    end
end

def is_susceptible(n: node) -> bool:
    return n.c1.state == S
end

def is_positive_inschool_node(n: node) -> bool:
    return (
        (n.c1.state == Ipresymp or n.c1.state == Isymp or n.c1.state == Iasymp)
        and n.is_in_school
        and not is_isolating(n)
    )
end

def set_seed(n: node):
    n.c1.state = E
end

def start_isolation_a(n: node):
    n.home_isolation_start = cur_tick+1
    n.home_isolation_end = cur_tick+15
end

def start_isolation_b(n: node):
    n.home_isolation_start = cur_tick+2
    n.home_isolation_end = cur_tick+16
end

def get_node_state(n: node) -> c1_state_t:
    return n.c1.state
end

nodeset susceptibles
nodeset seed_nodes
nodeset positive_inschool_nodes
nodeset will_start_isolation

def main():
    while cur_tick < num_ticks:
        cur_tick += 1
        print("cur_tick = ", cur_tick)

        c1.step(cur_tick, 1.0)

        if cur_tick < 10:
            select susceptibles using is_susceptible
            select seed_nodes sample approx 5 from susceptibles
            foreach node in seed_nodes run set_seed
        end

        select susceptibles using is_susceptible

        if (enable_day_30_antigen_test or enable_day_30_pcr_test) and cur_tick >= 30:
            select positive_inschool_nodes using is_positive_inschool_node
        end

        if enable_day_30_antigen_test and cur_tick >= 30:
            select will_start_isolation sample relative 0.8 from positive_inschool_nodes
            foreach node in will_start_isolation run start_isolation_a
        end

        if enable_day_30_pcr_test and cur_tick >= 30:
            select will_start_isolation sample relative 0.95 from positive_inschool_nodes
            foreach node in will_start_isolation run start_isolation_b
        end
    end
end
